// ****************************************************************************************************************************************************************
// ****************************************************************************************************************************************************************
//
//		File:		player.agc
//		Purpose:	Handles translation from fret/string ID to notes and playing.
//		Date:		3rd August 2016
//		Author:		Paul Robson (paul@robsons.org.uk)
//
// ****************************************************************************************************************************************************************
// ****************************************************************************************************************************************************************

// ****************************************************************************************************************************************************************
//															Convert string/fret to a note index, from 1 up.
// ****************************************************************************************************************************************************************

function PLAYERGetNoteIndex(stringID as integer,fret as integer)
	n = (stringID-1) * 5 + fret + 1
endfunction n

// ****************************************************************************************************************************************************************
//															Convert note index to a note with .5 for a sharp, based at E/0 E/7 E/14 etc.
// ****************************************************************************************************************************************************************

function PLAYERConvertToNoteSharp(note as integer)
	octave = (note - 1) / 12 																	// Octave number starting at 1
	n = mod(note - 1,12)+1 																		// Position in note
	note# = ValFloat(GetStringToken("0,1,1.5,2,2.5,3,3.5,4,5,5.5,6,6.5",",",n))		
	note# = note# + octave * 7
	//note# = note
endfunction note# 

// ****************************************************************************************************************************************************************
//													Convert a white.sharp format float to a name
// ****************************************************************************************************************************************************************

function PLAYERConvertToName(note# as float)
	name$ = mid("EFGABCD",mod(Floor(note#),7)+1,1)												// Get name from white part
	if note# <> floor(note#) then name$ = name$ + "#"											// add # if sharp
endfunction name$

// ****************************************************************************************************************************************************************
//																	Play a given note
// ****************************************************************************************************************************************************************

global __PLAYERLastSound as integer = 0															// Sound currently playing

function PLAYERPlay(note ref as Note)
	if __PLAYERLastSound <> 0 then StopSound(__PLAYERLastSound)									// Stop current sound
	__PLAYERLastSound = PLAYERGetNoteIndex(note.stringID,note.fret)								// Get new note
	PlaySound(__PLAYERLastSound)																// Play it.
endfunction

// ****************************************************************************************************************************************************************
//																Preload bass guitar sounds
// ****************************************************************************************************************************************************************

function PLAYERLoadSound()
	for note = 1 to 38
		LoadSoundOGG(note,SFXDIR+str(note)+".ogg")
	next note
endfunction
